# == OHMSBY Style QOL Patches v0.0.3 == #
#
#	by dionednd
#

# == Changelog == #
#
#	v0.0.1
#	- added 7 QOL changes (first three options can work for other characters)
#	v0.0.1a
#	- fixed tilting not working
#	v0.0.1b
#	- changed astral heat text position and fixed astral heat text position for most characters that didn't have their astral heat text position fixed
#	v0.0.2
#	- added fix for burst icons in tag/simul teammode
#	- added option to disable ohmsby style action messages (counter,perfect,not over yet, negative penalty, etc.)
#	- added offset option for burst icons (you can now move your burst icons with this module!)
#	- added 2 styles for tag team mode burst icons (teamleader only / semi-trans for partners)
#	- added option to disable bonus strike mechanic from characters that have it
#	v0.0.2a
#	- removed option to disable ohmsby style action messages but provided an .air file that you can paste at the end of your character's .air file that disables action messages
#	v0.0.2b
#	- fixed burst X icon position not being positioned properly due to the burst icon not having an id.
#	v0.0.2c
#	- added fix for ultra burst background.
#	- fixed burst icon digitizer not showing up on the correct icon
#	v0.0.3
#	- added sound volume mixer for voice channel and sfx channel
# == Global States == #

[StateDef -4]

	# CONFIG

	# Voice Channel Volume. set to -1 to disable. affects only the voice channel.
	let voice_volume = 100;

	# SFX Channel Volume. set to -1 to disable. affects all channels if voice channel is set to -1.
	let sfx_volume = 75;

	# Hitpause Animations. Animates hitpauses, making gethit states look more alive than robotic.
	let animate_hitpause = 1;

	# Air Strafe Tilt. tilts the character back or forward depending on player velocity. looks cool and modern.
	let air_strafe_tilt = 1;

	# Air Hitstun Tilt. tilts the character back or forward depending on player velocity. looks cool and modern.
	let air_hitstun_tilt = 1;

	# Astral Text Repositioning. Fixes Astral Cut-In Text for 16:9 Ratio. does nothing for other ratios.
	let astral_text_repos = 1;

	# Astral Camera Fix. Fixes astral heat camera zoom by normalizing it back to 1.0, preventing the camera from showing outside boundaries of background sprites.
	let astral_camera_fix = 1;

	# Astral Refresh. Allows you to use Astral Heat once per round.
	let astral_refresh = 0;

	# Burst Refresh. refreshes your burst every round.
	let burst_refresh = 0;

	if $sfx_volume != -1
	{
		modifySnd{channel:-1;volume:$sfx_volume;}
	}

	if $voice_volume != -1
	{
		modifySnd{channel:0;volume:$voice_volume;}
	}

	if $animate_hitpause && MoveType = H
	{
		AssertSpecial{flag:AnimateHitpause}
	}

	# Astral Cut In Text for 16:9 Ratio
	if roundstate = 0 && map(BBTAG_ACI_Repos) && $astral_text_repos
	{
		map(BBTAG_ACI_Repos) := 0;
	}

	if NumExplod(7008) > 0 && !map(BBTAG_ACI_Repos)  && gamewidth/gameheight = [1.77, 1.78] && $astral_text_repos
	{
		for i=0; NumExplod(7008); 1
		{
			if ExplodVar(7008, $i,anim) = 7009
			{
				ModifyExplod{id:7008;index:$i;pos:25, 140;postype:left}
				map(BBTAG_ACI_Repos) := 1;
			}
		}
	}

	if statetype=A && roundstate = 2
	{
		if (movetype=I && $air_strafe_tilt)
		{
			# use vel x and vel y to determine angle. tilt slightly forward if vel x is positive, tilt slightly backward if vel x is positive. idk how vel y should affect the tilting, but you are the smart one here, so you do the calculations.
			AngleSet{value:clamp((vel x * -3.0) + (vel y * 0.25), -18, 18)}
		}
		else if (movetype=H && $air_hitstun_tilt)
		{
			AngleSet{value:clamp((vel x * -.35) + (vel y * (vel x*-.35)), -180, 180)}
		}
		else if movetype=A {AngleSet{value:0}}
		AngleDraw{}
	}

	# Astral Camera Fix

	if roundstate = 0 && map(astraldone)
	{
		map(astraldone) := 1;
	}

	if var(20) = 1 && stateno = [3900,3909] && $astral_camera_fix
	{
		Zoom{scale:1.0 / camerazoom;camerabound:0;stagebound:1;lag:0;}
	}

	# Astral Refresh

	if $astral_refresh && roundstate = 0 && var(21) = 1
	{
		var(21) := 0;
	}

	# Burst Refresh
	if $burst_refresh && RoundsExisted > 0 && var(58) < 10000 && roundstate < 2
	{
		var(58) := 10000;
	}

[StateDef +1]

	# Fix Tag/Simul Burst Icons. Fixes overlapping burst icons in tag and simul.
	let burst_icon_fix = 1;

	# Tag Burst Icon Style. (0 = show teamleader's only, 1 = show all burst icons but have tagged out players' burst icons semi-transparent)
	let burst_icon_tagstyle = 1;

	# Fix Ultra Burst & Distortion Drive Background
	let ubdd_fix = 1;

	if $ubdd_fix
	{
		for idx = 0; numExplod - 1; 1
		{
			if ExplodVar(-1, $idx, anim) = 7001
			{
				ModifyExplod{id:-1;index:$idx;scale:.65,.4875;pos:160,90;postype:left}
			} 
		}
	}

	# Burst Icon Offset (Burst Icon Fix must be enabled.)
	let burst_icon_offsetx_p1 = 0;
	let burst_icon_offsety_p1 = 0;

	let burst_icon_offsetx_p2 = 0;
	let burst_icon_offsety_p2 = 0;

	let burst_icon_offsetx_p3 = 0;
	let burst_icon_offsety_p3 = 0;

	let burst_icon_offsetx_p4 = 0;
	let burst_icon_offsety_p4 = 0;

	let burst_icon_offsetx_p5 = 0;
	let burst_icon_offsety_p5 = 0;

	let burst_icon_offsetx_p6 = 0;
	let burst_icon_offsety_p6 = 0;

	let burst_icon_offsetx_p7 = 0;
	let burst_icon_offsety_p7 = 0;

	let burst_icon_offsetx_p8 = 0;
	let burst_icon_offsety_p8 = 0;

	# Disable Bonus Strike Mechanic. disables the bonus strike mechanic & action messages from some ohmsby style characters.
	let disable_bonus_strike = 1;

	ignorehitpause if (authorname = "Ichida" || authorname = "Vinnie" || authorname = "TornilloOxidado") && $disable_bonus_strike
	{
		var(28):= 0;
		for i=0; NumExplod(13000); 1
		{
			if ExplodVar(13000,$i,anim) = [9900,9902]
			{
				ModifyExplod{id:13000;index:$i;pos:9999,9999;postype:back}
			}
		}
	}

	ignorehitpause if isHelper(9875)
	{
		switch(root, playerno)
		{
			case 1:
				let ox = $burst_icon_offsetx_p1;
				let oy = $burst_icon_offsety_p1;
			case 2:
				let ox = $burst_icon_offsetx_p2;
				let oy = $burst_icon_offsety_p2;
			case 3:
				let ox = $burst_icon_offsetx_p3;
				let oy = $burst_icon_offsety_p3;
			case 4:
				let ox = $burst_icon_offsetx_p4;
				let oy = $burst_icon_offsety_p4;
			case 5:
				let ox = $burst_icon_offsetx_p5;
				let oy = $burst_icon_offsety_p5;
			case 6:
				let ox = $burst_icon_offsetx_p6;
				let oy = $burst_icon_offsety_p6;
			case 7:
				let ox = $burst_icon_offsetx_p7;
				let oy = $burst_icon_offsety_p7;
			case 8:
				let ox = $burst_icon_offsetx_p8;
				let oy = $burst_icon_offsety_p8;
		}
	}

	ignorehitpause if isHelper(9875) && $burst_icon_fix && playerno < 9
	{
		# Disable Burst Icon Digitizer, then just replace em with a clone that works for this module instead of trying to modify the original burst icon digitizer.
		modifyExplod{id: 10020; index: 0;bindtime:0;removetime:0;}
		modifyExplod{id: 10020; index: 1;bindtime:0;removetime:0;}
		if root, teammode = Tag
		{
			if $burst_icon_tagstyle = 0
			{
				if numExplod > 0
				{
					for idx = 0; numExplod - 1; 1
					{
						if ExplodVar(-1, $idx, anim) = 98755
						{
							modifyExplod{id: -1; index: $idx; pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
						} 
					}
				}
				ModifyExplod{id:98756;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:9876;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98765;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98766;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:9879;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98799;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:9878;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98788;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox)+((root,fvar(19))*0.046),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:9875;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98798;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox)+((root,fvar(19))*0.046),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98797;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				ModifyExplod{id:98758;index:0;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				if root,fvar(35) <= 0 && root,fvar(19) <= 0 && !NumHelper(2957) && root,var(50) = 1 && root,var(58) < 10000 && NumExplod(100200)<2
				{
					Explod{id:100200;anim:10020+(floor(root,Var(58)/50%200));scale:0.25,0.25;facing:cond(teamside = 1,1,-1);sprpriority:-7;bindtime:-1;removetime:1;pausemovetime:-1;supermovetime:-1;ownpal:1;pos:cond(root,playerno = root,TeamLeader,(12+$ox),9999),cond(root,playerno = root,TeamLeader,(45+$oy),9999);postype:back}
				}
			}
			else if $burst_icon_tagstyle = 1
			{
				if numExplod > 0
				{
					for idx = 0; numExplod - 1; 1
					{
						if ExplodVar(-1, $idx, anim) = 98755
						{
							modifyExplod{id: -1; index: $idx; pos:(12+$ox),(45+$oy) + 10 * (root,playerno - root,teamside);postype:back}
						} 
					}
				}
				ModifyExplod{id:98756;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;}
				ModifyExplod{id:9876;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:98765;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:98766;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:9879;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:98799;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,256,256)}
				ModifyExplod{id:9878;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:98788;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:9875;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,0,128)}
				ModifyExplod{id:98798;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,256,256)}
				ModifyExplod{id:98797;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:addalpha;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,256,256)}
				ModifyExplod{id:98758;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back;trans:sub;alpha:cond(playerno = TeamLeader,256,128),cond(playerno = TeamLeader,256,256)}
				if root,fvar(35) <= 0 && root,fvar(19) <= 0 && !NumHelper(2957) && root,var(50) = 1 && root,var(58) < 10000 && NumExplod(100200)<2
				{
					Explod{id:100200;anim:10020+(floor(root,Var(58)/50%200));scale:0.25,0.25;facing:cond(teamside = 1,1,-1);sprpriority:-7;bindtime:-1;removetime:1;pausemovetime:-1;supermovetime:-1;ownpal:1;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
				}
			}
		}
		else if root, teammode = Simul
		{
			if numExplod > 0
			{
				for idx = 0; numExplod - 1; 1
				{
					if ExplodVar(-1, $idx, anim) = 98755
					{
						modifyExplod{id: -1; index: $idx; pos:(12+$ox),(45+$oy) + 10 * (root,playerno - root,teamside);postype:back}
					} 
				}
			}
			ModifyExplod{id:98756;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:9876;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98765;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98766;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:9879;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98799;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:9878;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98788;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:9875;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98798;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98797;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			ModifyExplod{id:98758;index:0;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			if root,fvar(35) <= 0 && root,fvar(19) <= 0 && !NumHelper(2957) && root,var(50) = 1 && root,var(58) < 10000 && NumExplod(100200)<2
			{
				Explod{id:100200;anim:10020+(floor(root,Var(58)/50%200));scale:0.25,0.25;facing:cond(teamside = 1,1,-1);sprpriority:-7;bindtime:-1;removetime:1;pausemovetime:-1;supermovetime:-1;ownpal:1;pos:(12+$ox),(45+$oy) + 10 * (playerno - teamside);postype:back}
			}
		}
		else
		{
			if numExplod > 0
			{
				for idx = 0; numExplod - 1; 1
				{
					if ExplodVar(-1, $idx, anim) = 98755
					{
						modifyExplod{id: -1; index: $idx; pos:(12+$ox),(45+$oy);postype:back}
					} 
				}
			}
			ModifyExplod{id:98756;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:9876;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98765;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98766;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:9879;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98799;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:9878;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98788;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy);postype:back}
			ModifyExplod{id:9875;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98798;index:0;pos:(12+$ox)+((root,fvar(19))*0.046),(45+$oy);postype:back}
			ModifyExplod{id:98797;index:0;pos:(12+$ox),(45+$oy);postype:back}
			ModifyExplod{id:98758;index:0;pos:(12+$ox),(45+$oy);postype:back}
			if root,fvar(35) <= 0 && root,fvar(19) <= 0 && !NumHelper(2957) && root,var(50) = 1 && root,var(58) < 10000 && NumExplod(100200)<2
			{
				Explod{id:100200;anim:10020+(floor(root,Var(58)/50%200));scale:0.25,0.25;facing:cond(teamside = 1,1,-1);sprpriority:-7;bindtime:-1;removetime:1;pausemovetime:-1;supermovetime:-1;ownpal:1;pos:(12+$ox),(45+$oy);postype:back}
			}
		}
	}
